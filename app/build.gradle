apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "28.0.2"

    defaultConfig {
        applicationId "com.madgaze.madmagcalibrate"
        minSdkVersion 24
        targetSdkVersion 24
        versionCode 1000000
        versionName "1.0.0"
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java', 'src/main/aidl']
            resources.srcDirs = ['src/main/java', 'src/main/aidl']
            aidl.srcDirs = ['src/main/aidl']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    flavorDimensions 'cpuArch'
    productFlavors {
        arm7 {
            dimension 'cpuArch'
            ndk {
                abiFilter 'armeabi-v7a'
            }
        }
        universal {
            dimension 'cpuArch'
            // include all default ABIs. with NDK-r16,  it is:
            //   armeabi-v7a, arm64-v8a, x86, x86_64
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    applicationVariants.all { variant ->
        def isRelease = variant.buildType.name.equalsIgnoreCase('release')
        if (isRelease) {
            variant.outputs.all { output ->
                def releaseApkName = "MadMagCalibrate-v${defaultConfig.versionName}-" + variant.flavorName.replace('_', '') + "-${getBuildTime()}" + '.apk'
                outputFileName = releaseApkName
            }
        } else {
            variant.outputs.all { output ->
                def debugApkName = "MadMagCalibrate-v${defaultConfig.versionName}-" + variant.flavorName.replace('_', '') + '-debug.apk'
                outputFileName = debugApkName
            }
        }
    }
}

def getBuildTime() {
    new Date().format("yyyy-MM-dd_HH-mm-ss", TimeZone.getTimeZone("GMT+8:00"))
}

dependencies {
    compileOnly files('libs/framework.jar')
    implementation 'com.android.support:support-v4:24+'
}
